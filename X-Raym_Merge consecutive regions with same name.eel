// Merge overlapping regions with same name
// EEL Script for Reaper
// Author : X-Raym
// Author URl : http://extremraym.com
// Source : GitHub > X-Raym > EEL Scripts for Cockos REAPER
// Source URl : https://github.com/X-Raym/REAPER-EEL-Scripts
// Licence : GPL v3
// Release Date : 03-01-2015

// Version : 0.5
// Version Date : 03-01-2015
// Required : Reaper 4.76

// From : "name" variable in RPR_EnumProjectMarkers3 - Cockos Confederated Forums
// http://forum.cockos.com/showthread.php?t=136047

function msg(m)
(
	ShowConsoleMsg(m);
	ShowConsoleMsg("\n");
);

function msg_d(m)
(
	sprintf(str, "%d", m);
	ShowConsoleMsg(str);
	ShowConsoleMsg("\n");
);

function msg_f(m)
(
	sprintf(str, "%f", m);
	ShowConsoleMsg(str);
	ShowConsoleMsg("\n");
);

function merge_overlapping_regions()
(

	i = 0;

	CountProjectMarkers(NULL, num_markersOut, num_regionsOut);
	regions_number_before = num_regionsOut;
	
	merged_regions_total = 0;

	ShowConsoleMsg("=====>\n");
	ShowConsoleMsg("INSTRUCTIONS : Execute the action several times until the number of regions which have been merge become 0.\n");
	ShowConsoleMsg("BEWARE : If you have regions subtitles, you will have to re-generate them!\n");
	ShowConsoleMsg("-----\n");

	consecutive_threshold = 2.5; // You can edit this ! The value is in seconds.
	ShowConsoleMsg("Threshold (in seconds): ");
	msg_f(consecutive_threshold);
	ShowConsoleMsg("-----\n");

	// EEL: int EnumProjectMarkers(int idx, bool &isrgnOut, &posOut, &rgnendOut, #nameOut, int &markrgnindexnumberOut)
	while (EnumProjectMarkers(i, is_region, pos, rgn_end, #name, markrgn_index_number)) (    

	region_start = pos;
	region_end = rgn_end;
	region_id = markrgn_index_number;
		
	region_name = #name;
	region_name_string = sprintf(#dest, "%s", region_name);

	compare_name = strcmp(region_name_string, region_name_previous_string); // strcmp(str, str2) -- compares str to str2, case sensitive, returns -1, 0, or 1

	region_end_previous_threshold = region_end_previous + consecutive_threshold;

	// You can edit threshold right below !
		region_end_previous_threshold >= region_start ? (
			compare_name === 0 ? (

				//Here you can add infos about deleted regions.
				//ShowConsoleMsg("Deleted regions: \n");
				//msg(#name);
				//msg_d(markrgn_index_number);
				//msg_d(is_region);
				//msg_f(pos);
				//msg_f(rgn_end);
				//ShowConsoleMsg("-----\n");

				SetProjectMarker(region_previous_id, 1, region_start_previous, region_end, region_name_previous_string);
				DeleteProjectMarker(NULL, region_id, 1);
		
				merged_regions_total += 1;

		);

	);

		region_start_previous = pos;
		region_end_previous = rgn_end;
		region_name_previous = #name;
		region_name_previous_string = sprintf(#dest2, "%s", region_name_previous);
		region_previous_id = markrgn_index_number;

		i += 1;

	);
	
	CountProjectMarkers(NULL, num_markersOut, num_regionsOut);
	regions_number_after = num_regionsOut;

	ShowConsoleMsg("Regions before the script: \n");
	msg_d(regions_number_before);
	ShowConsoleMsg("Regions after the script: \n");
	msg_d(regions_number_after);
	ShowConsoleMsg("Total of regions which have been merged: \n");
	msg_d(merged_regions_total);
	
	merged_regions_total > 0 ? (
		ShowConsoleMsg("Please execute the script again.\n");
	):(
		ShowConsoleMsg("You can close the console.\n");
	);
	
	ShowConsoleMsg("<=====\n\n");

);

merge_overlapping_regions();